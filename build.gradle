def artioJavaVersion = '1.8'
def artioFixVersion = '0.114'

defaultTasks 'clean', 'build'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    tasks.withType(JavaCompile) {
        sourceCompatibility = artioJavaVersion
        targetCompatibility = artioJavaVersion
        options.encoding = 'UTF-8'
        options.deprecation = true

        test {
            useJUnitPlatform()
        }
        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

configure([project(':fix-44-codecs'), project(':fix-50sp2-codecs')]) {

    dependencies {
        implementation "uk.co.real-logic:artio-codecs:${artioFixVersion}"
    }

    compileJava {
        dependsOn 'generateCodecs'
    }

}

configure([project(':microservice')]) {
    dependencies {
        implementation 'org.agrona:agrona:1.14.0'
        implementation 'io.aeron:aeron-client:1.37.0'
        implementation 'io.aeron:aeron-driver:1.37.0'
    }
}

project(':fix-44-codecs') {
    def generatedDir = project.convention.plugins.java.sourceSets.main.java.srcDirs.first()

    task generateCodecs(type: JavaExec) {
        systemProperty "fix.codecs.parent_package", "com.swiftalgo.ms.codecs.fix44"
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.compileClasspath
        args = [generatedDir, 'src/main/resources/FIX44.xml']
        inputs.file('src/main/resources/FIX44.xml')
        outputs.dir generatedDir
    }
}

project(':fix-50sp2-codecs') {
    def generatedDir = project.convention.plugins.java.sourceSets.main.java.srcDirs.first()

    task generateCodecs(type: JavaExec) {
        systemProperty "fix.codecs.parent_package", "com.swiftalgo.ms.codecs.fix50sp2"
        main = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.compileClasspath
        args = [generatedDir, 'src/main/resources/FIXT11.xml;src/main/resources/FIX50SP2.xml']
        inputs.files('src/main/resources/FIXT11.xml', 'src/main/resources/FIX50SP2.xml')
        outputs.dir generatedDir
    }
}

wrapper {
    gradleVersion = '6.8.3'
    distributionType = 'ALL'
}

