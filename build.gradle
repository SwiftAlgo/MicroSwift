def artioJavaVersion = '1.8'

defaultTasks 'clean', 'build'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    tasks.withType(JavaCompile) {
        sourceCompatibility = artioJavaVersion
        targetCompatibility = artioJavaVersion
        options.encoding = 'UTF-8'
        options.deprecation = true

        test {
            useJUnitPlatform()
        }
        dependencies {
            testImplementation libs.junit.jupiter.api
            testRuntimeOnly libs.junit.jupiter.engine
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

configure([project(':codecs:fix-44-codecs'), project(':codecs:fix-50sp2-codecs')]) {

    dependencies {
        implementation libs.artio.codecs
    }

    compileJava {
        dependsOn 'generateCodecs'
    }

}

configure([project(':microservice')]) {
    dependencies {
        implementation libs.agrona
        implementation libs.aeron.client
        implementation libs.aeron.driver
        implementation project(':codecs:fix-44-codecs')
        implementation libs.artio.codecs
    }
}

project(':codecs:fix-44-codecs') {
    def generatedDir = sourceSets.main.java.srcDirs.first()

    task generateCodecs(type: JavaExec) {
        systemProperty "fix.codecs.parent_package", "com.swiftalgo.ms.codecs.fix44"
        mainClass = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.compileClasspath
        args = [generatedDir, 'src/main/resources/FIX44.xml']
        inputs.file('src/main/resources/FIX44.xml')
        outputs.dir generatedDir
    }
}

project(':codecs:fix-50sp2-codecs') {
    def generatedDir = sourceSets.main.java.srcDirs.first()

    task generateCodecs(type: JavaExec) {
        systemProperty "fix.codecs.parent_package", "com.swiftalgo.ms.codecs.fix50sp2"
        mainClass = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
        classpath = sourceSets.main.compileClasspath
        args = [generatedDir, 'src/main/resources/FIXT11.xml;src/main/resources/FIX50SP2.xml']
        inputs.files('src/main/resources/FIXT11.xml', 'src/main/resources/FIX50SP2.xml')
        outputs.dir generatedDir
    }
}

wrapper {
    gradleVersion = '7.3.3'
    distributionType = 'ALL'
}

